# chezmoi:template:left-delimiter="# {{" right-delimiter=}}
# {{ if .wsl -}}
fish_add_path --path --prepend "$HOME/.local/bin"

# Remove unwanted PATH entries
begin
    string match -i "/mnt/c/Users/*/scoop/apps/mise/current/mise/shims" $PATH
    string match -i "/mnt/c/Program Files (x86)/Paragon Software/APFS for Windows/" $PATH
    string match -i "/mnt/c/Program Files/Eclipse Adoptium/jdk-*/bin" $PATH
    string match -i "/mnt/c/Users/*/scoop/apps/openjdk/current/bin" $PATH
    begin
        string match /bin $PATH
        string match /sbin $PATH
        string match /usr/sbin $PATH
    end | path filter --type=link
end | while read -l match
    set -l index (contains -i -- $match $PATH)
    set -e PATH[$index]
end

# Get systemd environment variables (excluding $PATH)
if [ -x /usr/lib/systemd/user-environment-generators/30-systemd-environment-d-generator ]
    /usr/lib/systemd/user-environment-generators/30-systemd-environment-d-generator | string match -v 'PATH=*' | while read -l var
        export $var
    end
end

# Set environment variables
# Will eventually be redone to utilize universal variables
# set -gx ANSIBLE_CONFIG $XDG_CONFIG_HOME/ansible.cfg
# set -gx ANSIBLE_HOME $XDG_CONFIG_HOME/ansible
# set -gx ZDOTDIR $XDG_CONFIG_HOME/zsh
# {{ end -}}
