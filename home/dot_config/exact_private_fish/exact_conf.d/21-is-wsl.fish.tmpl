# chezmoi:template:left-delimiter="# {{" right-delimiter=}}
# {{ if .device.wsl -}}
fish_add_path --path --prepend "$HOME/.local/bin"

# Remove unwanted PATH entries
begin
    # {{- range .settings.shell.fish.wsl.path_remove }}
    string match -i "# {{.}}" $PATH
    # {{- end }}
    begin
        string match /bin $PATH
        string match /sbin $PATH
        string match /usr/sbin $PATH
    end | path filter --type=link
end | while read -l match
    set -l index (contains -i -- $match $PATH)
    set -e PATH[$index]
end

# Get systemd environment variables (excluding $PATH)
if [ -x /usr/lib/systemd/user-environment-generators/30-systemd-environment-d-generator ]
    /usr/lib/systemd/user-environment-generators/30-systemd-environment-d-generator | string match -v 'PATH=*' | while read -l var
        export $var
    end
end

# {{ end -}}
